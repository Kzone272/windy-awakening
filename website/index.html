<html>
  <head>
  	<link rel="stylesheet" type="text/css" href="css/style.css" />
    <script type="text/javascript" src="scripts/lib/gl-matrix.js"></script>

<script id="voronoi-vs" type="x-shader/x-vertex">
attribute vec3 aPos;

uniform mat4 uM;
uniform mat4 uP;

void main() {
  gl_Position = uP * uM * vec4(aPos, 1);
}
</script>
<script id="voronoi-fs" type="x-shader/x-fragment">
precision mediump float;

uniform vec3 uCol;

void main() {
  gl_FragColor = vec4(uCol, 1);
}
</script>

<script id="edges-vs" type="x-shader/x-vertex">
attribute vec3 aPos;
attribute vec2 aTexCoord;

varying highp vec2 vTexCoord;

void main() {
  gl_Position = vec4(aPos, 1);
  vTexCoord = aTexCoord;
}
</script>
<script id="edges-fs" type="x-shader/x-fragment">
precision highp float;

varying highp vec2 vTexCoord;
uniform int uWidth;
uniform int uHeight;
uniform sampler2D uTexture;

void main() {
  float hstep = 1.0 / float(uWidth);
  float vstep = 1.0 / float(uHeight);

  vec3 up = texture2D(uTexture, vec2(vTexCoord.s, max(vTexCoord.t - vstep, 0.0))).rgb;
  vec3 down = texture2D(uTexture, vec2(vTexCoord.s, min(vTexCoord.t + vstep, 1.0))).rgb;
  vec3 left = texture2D(uTexture, vec2(max(vTexCoord.s - hstep, 0.0), vTexCoord.t)).rgb;
  vec3 right = texture2D(uTexture, vec2(min(vTexCoord.s + hstep, 1.0), vTexCoord.t)).rgb;

  float I = (distance(up, down) + distance(left, right)) / 2.0;

  if (I > 0.0) {
    I = 1.0;
  } else {
    I = 0.0;
  }

  gl_FragColor = vec4(vec3(I), 1);
}
</script>

<script id="blur-vs" type="x-shader/x-vertex">
attribute vec3 aPos;
attribute vec2 aTexCoord;

varying highp vec2 vTexCoord;

void main() {
  gl_Position = vec4(aPos, 1);
  vTexCoord = aTexCoord;
}

</script>
<script id="blur-fs" type="x-shader/x-fragment">

precision highp float;

varying highp vec2 vTexCoord;
uniform int uWidth;
uniform int uHeight;
uniform vec2 uDir;
uniform sampler2D uTexture;

void main() {
  float blur = 15.0;
  const int kernel = 20;

  float hstep = blur * uDir.x / float(uWidth) / float(2 * kernel + 1);
  float vstep = blur * uDir.y / float(uHeight) / float(2 * kernel + 1);

  vec4 sum = vec4(0.0);
  for (int i = -kernel; i < kernel + 1; i++) {
    float x = clamp(vTexCoord.s + float(i) * hstep, 0.0, 1.0);
    float y = clamp(vTexCoord.t + float(i) * vstep, 0.0, 1.0);
    sum += texture2D(uTexture, vec2(x, y));
  }

  gl_FragColor = vec4(sum.rgb / float(2 * kernel + 1), 1.0);
}
</script>

<script id="water-vs" type="x-shader/x-vertex">
attribute vec3 aPos;
attribute vec2 aTexCoord;

varying highp vec2 vTexCoord;

void main() {
  gl_Position = vec4(aPos, 1);
  vTexCoord = aTexCoord;
}
</script>
<script id="water-fs" type="x-shader/x-fragment">
precision highp float;

varying highp vec2 vTexCoord;
uniform sampler2D uTexture;

void main() {
  vec3 colour = texture2D(uTexture, vTexCoord).rgb;
  vec3 blue = vec3(0.0, 0.56, 1.0);
  float waves = 0.3;

  if (length(colour) > 0.1) {
    gl_FragColor = vec4((1.0 - waves) * blue + waves * vec3(1), 1);
  } else {
    gl_FragColor = vec4(blue, 1);
  }
}
</script>

<script id="scene-vs" type="x-shader/x-vertex">
attribute vec3 aPos;
attribute vec3 aNorm;
attribute vec2 aTexCoord;

uniform mat4 uModelView;
uniform mat4 uNormalMatrix;
uniform mat4 uP;

varying highp vec3 vPos;
varying highp vec3 vNorm;
varying highp vec2 vTexCoord;

void main() {
  gl_Position = uP * uModelView * vec4(aPos, 1);

  vPos = vec3(uModelView * vec4(aPos, 1));
  vNorm = vec3(uNormalMatrix * vec4(aNorm, 1));
  vTexCoord = aTexCoord;
}
</script>
<script id="scene-fs" type="x-shader/x-fragment">
precision highp float;

varying highp vec3 vPos;
varying highp vec3 vNorm;
varying highp vec2 vTexCoord;

uniform vec3 uLightDir;
uniform sampler2D uTexture;

void main() {
  vec3 kd = texture2D(uTexture, vTexCoord).rgb;

  vec3 E = normalize(-vPos);
  vec3 L = normalize(uLightDir - vPos);
  vec3 N = normalize(vNorm);
  vec3 up = normalize(vec3(0, 1, 0));

  float ki = 0.7 * max(dot(N, L), 0.0) + 0.3;

  if (ki > 0.3) {
    ki = 1.0;
  } else {
    ki = 0.6;
  }

  gl_FragColor = vec4(clamp(ki * kd, 0.0, 1.0), 1);
}
</script>

  </head>
  <body>
    <div class="controls">
      <form>
        <input type="checkbox" checked id="edges" name="edges" />
        <label for="edges">Detect Edges</label>
        <input type="checkbox" checked id="blur" name="blur" />
        <label for="blur">Blur</label>
        <input type="checkbox" checked id="water" name="water" />
        <label for="water">Water Colouring</label>
      </form>
    </div>
  	<div class="canvasContainer">
  	  <canvas id="window" width="1280" height="720"></canvas>
    </div>

    <script type="text/javascript" src="scripts/objParser.js"></script>
    <script type="text/javascript" src="scripts/main.js"></script>
  </body>
</html>