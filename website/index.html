<html>
  <head>
  	<link rel="stylesheet" type="text/css" href="css/style.css" />
    <script type="text/javascript" src="scripts/lib/gl-matrix.js"></script>

<script id="perlin-vs" type="x-shader/x-vertex">
attribute vec3 aPos;

void main() {
  gl_Position = vec4(aPos, 1);
}
</script>
<script id="perlin-fs" type="x-shader/x-fragment">
precision highp float;

// Returns a random float in the range -1 to 1
float rand(vec2 co){
  return 2.0 * fract(sin(dot(co.xy ,vec2(12.9898,78.233))) * 43758.5453) - 1.0;
}

float fade(float t) {
  return t*t*t*(t*(t*6.0-15.0)+10.0);
}

float perlin(vec2 P) {
  vec2 p = fract(P);
  vec2 zone = floor(P);

  vec2 g00 = vec2(rand(zone + vec2(0, 0)), rand(zone + vec2(1, 1)));
  vec2 g01 = vec2(rand(zone + vec2(0, 1)), rand(zone + vec2(1, 2)));
  vec2 g10 = vec2(rand(zone + vec2(1, 0)), rand(zone + vec2(2, 1)));
  vec2 g11 = vec2(rand(zone + vec2(1, 1)), rand(zone + vec2(2, 2)));

  vec2 s00 = p;
  vec2 s01 = p - vec2(0.0, 1.0);
  vec2 s10 = p - vec2(1.0, 0.0);
  vec2 s11 = p - vec2(1.0, 1.0);

  float q00 = dot(g00, s00);
  float q01 = dot(g01, s01);
  float q10 = dot(g10, s10);
  float q11 = dot(g11, s11);

  vec2 x = mix(vec2(q00, q01), vec2(q10, q11), fade(p.x));
  float xy = mix(x.x, x.y, fade(p.y));

  return xy;
}

void main() {
  float sum = 0.0;

  for (int i = 0; i < 3; i++) {
    sum += perlin(pow(2.0, float(i)) * gl_FragCoord.xy / 256.0);
  }

  gl_FragColor = vec4(vec3(sum) / 2.0 + 0.5, 1);
}
</script>

<script id="voronoi-vs" type="x-shader/x-vertex">
attribute vec3 aPos;

uniform mat4 uM;
uniform mat4 uP;

void main() {
  gl_Position = uP * uM * vec4(aPos, 1);
}
</script>
<script id="voronoi-fs" type="x-shader/x-fragment">
precision mediump float;

uniform vec3 uCol;

void main() {
  gl_FragColor = vec4(uCol, 1);
}
</script>

<script id="edges-vs" type="x-shader/x-vertex">
attribute vec3 aPos;
attribute vec2 aTexCoord;

varying highp vec2 vTexCoord;

void main() {
  gl_Position = vec4(aPos, 1);
  vTexCoord = aTexCoord;
}
</script>
<script id="edges-fs" type="x-shader/x-fragment">
precision highp float;

varying highp vec2 vTexCoord;
uniform int uWidth;
uniform int uHeight;
uniform sampler2D uTexture;

void main() {
  float hstep = 1.0 / float(uWidth);
  float vstep = 1.0 / float(uHeight);

  vec3 up = texture2D(uTexture, vec2(vTexCoord.s, max(vTexCoord.t - vstep, 0.0))).rgb;
  vec3 down = texture2D(uTexture, vec2(vTexCoord.s, min(vTexCoord.t + vstep, 1.0))).rgb;
  vec3 left = texture2D(uTexture, vec2(max(vTexCoord.s - hstep, 0.0), vTexCoord.t)).rgb;
  vec3 right = texture2D(uTexture, vec2(min(vTexCoord.s + hstep, 1.0), vTexCoord.t)).rgb;

  float I = (distance(up, down) + distance(left, right)) / 2.0;

  if (I > 0.0) {
    I = 1.0;
  } else {
    I = 0.0;
  }

  gl_FragColor = vec4(vec3(I), 1);
}
</script>

<script id="blur-vs" type="x-shader/x-vertex">
attribute vec3 aPos;
attribute vec2 aTexCoord;

varying highp vec2 vTexCoord;

void main() {
  gl_Position = vec4(aPos, 1);
  vTexCoord = aTexCoord;
}

</script>
<script id="blur-fs" type="x-shader/x-fragment">

precision highp float;

varying highp vec2 vTexCoord;
uniform int uWidth;
uniform int uHeight;
uniform vec2 uDir;
uniform sampler2D uTexture;

void main() {
  float blur = 30.0;
  const int kernel = 15;

  float hstep = blur * uDir.x / float(uWidth) / float(2 * kernel + 1);
  float vstep = blur * uDir.y / float(uHeight) / float(2 * kernel + 1);

  vec4 sum = vec4(0.0);
  for (int i = -kernel; i < kernel + 1; i++) {
    float x = clamp(vTexCoord.s + float(i) * hstep, 0.0, 1.0);
    float y = clamp(vTexCoord.t + float(i) * vstep, 0.0, 1.0);
    sum += texture2D(uTexture, vec2(x, y));
  }

  gl_FragColor = vec4(sum.rgb / float(2 * kernel + 1), 1.0);
}
</script>

<script id="water-vs" type="x-shader/x-vertex">
attribute vec3 aPos;
attribute vec2 aTexCoord;

varying highp vec2 vTexCoord;

void main() {
  gl_Position = vec4(aPos, 1);
  vTexCoord = aTexCoord;
}
</script>
<script id="water-fs" type="x-shader/x-fragment">
precision highp float;

varying highp vec2 vTexCoord;
uniform sampler2D uTexture;

void main() {
  vec3 colour = texture2D(uTexture, vTexCoord).rgb;
  vec3 blue = vec3(0.0, 0.56, 1.0);
  float waves = 0.5;

  if (length(colour) > 0.03) {
    gl_FragColor = vec4((1.0 - waves) * blue + waves * vec3(1), 1);
  } else {
    gl_FragColor = vec4(blue, 1);
  }
}
</script>

<script id="shadow-vs" type="x-shader/x-vertex">
attribute vec3 aPos;

uniform mat4 uM;
uniform mat4 uV;
uniform mat4 uP;

void main() {
  gl_Position = uP * uV * uM * vec4(aPos, 1);
}
</script>
<script id="shadow-fs" type="x-shader/x-fragment">
precision highp float;

void main() {
  gl_FragColor = vec4(gl_FragCoord.z);
}
</script>

<script id="test-vs" type="x-shader/x-vertex">
attribute vec3 aPos;
attribute vec2 aTexCoord;

varying highp vec2 vTexCoord;

void main() {
  gl_Position = vec4(aPos, 1);
  vTexCoord = aTexCoord;
}
</script>
<script id="test-fs" type="x-shader/x-fragment">
precision highp float;

varying highp vec2 vTexCoord;
uniform sampler2D uTexture;

void main() {
  gl_FragColor = texture2D(uTexture, vTexCoord);
}
</script>

<script id="scene-vs" type="x-shader/x-vertex">
attribute vec3 aPos;
attribute vec3 aNorm;
attribute vec2 aTexCoord;

uniform mat4  uModelView;
uniform mat4  uNormalMatrix;
uniform mat4  uP;
uniform mat4  uBias;
uniform mat4  uLightV;
uniform mat4  uLightP;
uniform vec3  uBoatPos;
uniform float uTexScale;
uniform int   uFrame;
uniform bool  uIsWater;
uniform bool  uIsIsland;
uniform sampler2D uIslandHeight;

varying highp vec3 vPos;
varying highp vec3 vNorm;
varying highp vec2 vTexCoord;
varying highp vec4 vShadowCoord;

#define M_PI 3.1415926535897932384626433832795

float islandHeight(vec2 p) {
  return texture2D(uIslandHeight, p).g * 8.0 - 5.0;
}

void main() {
  vec3 pos = aPos;
  vec3 norm = aNorm;
  vec2 texCoord = aTexCoord;

  if (uIsWater) {
    pos.xz += uBoatPos.xz;
    texCoord += uBoatPos.xz / uTexScale;
    pos.y = 0.2 * sin(pos.x) * sin(pos.z + float(uFrame) / 100.0);
  }

  if (uIsIsland) {
    pos.y = islandHeight(texCoord);
    float offset = 0.0025;
    float xHeight = islandHeight(texCoord + vec2(offset, 0));
    float zHeight = islandHeight(texCoord + vec2(0, offset));
    float backward = islandHeight(texCoord + vec2(0, offset));
    norm = vec3(0, 0.5, 0) + normalize(cross(vec3(0, zHeight - pos.y, offset), vec3(offset, xHeight - pos.y, 0)));
  }

  vPos = vec3(uModelView * vec4(pos, 1));
  vNorm = vec3(uNormalMatrix * vec4(norm, 1));
  vTexCoord = texCoord;
  vShadowCoord = uBias * uLightP * uLightV * vec4(pos, 1);

  gl_Position = uP * uModelView * vec4(pos, 1);
}
</script>
<script id="scene-fs" type="x-shader/x-fragment">
precision highp float;

varying highp vec3 vPos;
varying highp vec3 vNorm;
varying highp vec2 vTexCoord;
varying highp vec4 vShadowCoord;

uniform vec3 uLightDir;
uniform sampler2D uShadowMap;
uniform sampler2D uTexture;
uniform sampler2D uIslandTexture;
uniform bool uIsWater;
uniform bool uIsIsland;

void main() {
  vec3 kd = texture2D(uTexture, vTexCoord).rgb;

  vec3 E = normalize(-vPos);
  vec3 L = normalize(-uLightDir);
  vec3 N = normalize(vNorm);
  vec3 UP = normalize(vec3(0, 1, 0));

  float ki = 0.7 * max(dot(N, L), 0.0) + 0.3;

  float shadow = texture2D(uShadowMap, vShadowCoord.xy).z;

  if (shadow != 1.0 && shadow < vShadowCoord.z) {
    ki = 0.6;
  } else {
    if (ki > 0.3) {
      ki = 1.0;
    } else {
      ki = 0.6;
    }
  }

  vec3 diffuse = ki * kd;

  vec3 rim = vec3(0);
  if (!uIsWater) {
    float sharpness = 3.0;
    float rimi = pow(1.0 - clamp(dot(N, E) + 0.3, 0.0, 1.0), sharpness);
    vec3 rimd = vec3(0.2, 0.7, 1.0);
    rim = rimi * rimd;
  }

  if (uIsIsland) {
    float fade = (1.0 - kd.r) * 2.5 + 0.1;
    vec3 isld = texture2D(uIslandTexture, vec2(fade, 0)).rgb;
    diffuse = ki * isld;
  }

  vec3 colour = diffuse + rim;
  colour = clamp(colour, 0.0, 1.0);

  gl_FragColor = vec4(colour, 1);
}
</script>

<script id="cutoff-vs" type="x-shader/x-vertex">
attribute vec3 aPos;
attribute vec2 aTexCoord;

varying highp vec2 vTexCoord;

void main() {
  gl_Position = vec4(aPos, 1);
  vTexCoord = aTexCoord;
}
</script>
<script id="cutoff-fs" type="x-shader/x-fragment">
precision highp float;

varying highp vec2 vTexCoord;
uniform sampler2D uTexture;

void main() {
  vec3 colour = texture2D(uTexture, vTexCoord).rgb;

  if (length(1.0 - colour) < 0.5) {
    gl_FragColor = vec4(colour, 1);
  } else {
    gl_FragColor = vec4(vec3(0), 1);
  }
}
</script>

<script id="bloom-vs" type="x-shader/x-vertex">
attribute vec3 aPos;
attribute vec2 aTexCoord;

varying highp vec2 vTexCoord;

void main() {
  gl_Position = vec4(aPos, 1);
  vTexCoord = aTexCoord;
}
</script>
<script id="bloom-fs" type="x-shader/x-fragment">
precision highp float;

varying highp vec2 vTexCoord;
uniform sampler2D uOriginal;
uniform sampler2D uShine;

void main() {
  vec3 original = texture2D(uOriginal, vTexCoord).rgb;
  vec3 shine = texture2D(uShine, vTexCoord).rgb;
  vec3 colour = clamp(original + shine, 0.0, 1.0);

  gl_FragColor = vec4(colour, 1);
}
</script>

  </head>
  <body>
    <div class="controls">
      <form>
        <input type="checkbox" checked id="edges" name="edges" />
        <label for="edges">Detect Edges</label>
        <input type="checkbox" checked id="blur" name="blur" />
        <label for="blur">Blur</label>
        <input type="checkbox" checked id="water" name="water" />
        <label for="water">Water Colouring</label>
        <input type="checkbox" id="mute" name="mute" />
        <label for="mute">Mute</label>
      </form>
    </div>
  	<div class="canvasContainer">
  	  <canvas id="window" width="1280" height="720"></canvas>
    </div>
    <audio id="oceanTheme" preload="auto" loop src="assets/sounds/ocean.mp3"></audio>

    <script type="text/javascript" src="scripts/objParser.js"></script>
    <script type="text/javascript" src="scripts/main.js"></script>
  </body>
</html>